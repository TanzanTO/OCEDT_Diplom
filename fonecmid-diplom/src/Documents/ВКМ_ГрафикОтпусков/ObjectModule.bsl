
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты) 

	Для каждого Строка Из ОтпускаСотрудников Цикл   
		
		Если ЗначениеЗаполнено(Строка.ДатаНачала) И ЗначениеЗаполнено(Строка.ДатаОкончания) Тогда 
			Если Строка.ДатаНачала > Строка.ДатаОкончания Тогда  
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон("В строке №%1 не верно заполнен период", Строка.НомерСтроки)); 
				Отказ = Истина; 
			КонецЕсли; 
			
		КонецЕсли; 
		
	КонецЦикла;  

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Движения.ВКМ_УчетОтпусков.Записывать = Истина;
				
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
			|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник КАК Сотрудник,
			|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания КАК ДатаОкончания,
			|	РАЗНОСТЬДАТ(ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала, ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания,
			|		ДЕНЬ) + 1 КАК КоличествоДней
			|ИЗ
			|	Документ.ВКМ_ГрафикОтпусков.ОтпускаСотрудников КАК ВКМ_ГрафикОтпусковОтпускаСотрудников
			|ГДЕ
			|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка = &Ссылка
			|ИТОГИ
			|	СУММА(КоличествоДней)
			|ПО
			|	Сотрудник";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	БазаОтпуска = 28;
	
	Пока Результат.Следующий() Цикл
		
		ПревышеноДней = Результат.КоличествоДней - БазаОтпуска;
		
		Если ПревышеноДней > 0 Тогда     
			
			Отказ = Истина;

			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Привышено количество дней отпуска в год по сотруднику %1", Результат.Сотрудник));  
			
		КонецЕсли; 
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;    
		
		РезультатДетально = Результат.Выбрать();
		
		Пока РезультатДетально.Следующий() Цикл
			
			Движение = Движения.ВКМ_УчетОтпусков.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = РезультатДетально.ДатаОкончания;
			Движение.Сотрудник = РезультатДетально.Сотрудник;
			Движение.Значение = РезультатДетально.КоличествоДней;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры 

#КонецОбласти

#КонецЕсли
