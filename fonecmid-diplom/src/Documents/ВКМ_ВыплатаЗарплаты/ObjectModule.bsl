
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения) 
	
	 // регистр ВКМ_ВзаиморасчетыССотрудниками
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Выплаты;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Сотрудник", "Сотрудник");
	Блокировка.Заблокировать();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				|	ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник КАК Сотрудник,
				|	СУММА(ВКМ_ВыплатаЗарплатыВыплаты.Сумма) КАК Сумма,
				|	ВКМ_ВыплатаЗарплатыВыплаты.ВидРасчета КАК ВидРасчета
				|ПОМЕСТИТЬ ВТ_Док
				|ИЗ
				|	Документ.ВКМ_ВыплатаЗарплаты.Выплаты КАК ВКМ_ВыплатаЗарплатыВыплаты
				|ГДЕ
				|	ВКМ_ВыплатаЗарплатыВыплаты.Ссылка = &Ссылка
				|СГРУППИРОВАТЬ ПО
				|	ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник,
				|	ВКМ_ВыплатаЗарплатыВыплаты.ВидРасчета
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ВТ_Док.Сотрудник КАК Сотрудник,
				|	ВТ_Док.Сумма КАК Сумма,
				|	ВТ_Док.ВидРасчета КАК ВидРасчета,
				|	ЕСТЬNULL(ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток, 0) КАК СуммаОстаток
				|ИЗ
				|	ВТ_Док КАК ВТ_Док
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&Дата, Сотрудник В
				|			(ВЫБРАТЬ
				|				ВТ_Док.Сотрудник КАК Сотрудник
				|			ИЗ
				|				ВТ_Док КАК ВТ_Док)) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки
				|		ПО ВТ_Док.Сотрудник = ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник";
	

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", МоментВремени());

	Результат= Запрос.Выполнить().Выбрать();

	Пока Результат.Следующий() Цикл
		Если Результат.СуммаОстаток = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("У Сотрудника %1 начислений нет", Результат.Сотрудник));
			Отказ = Истина;
		ИначеЕсли Результат.Сумма > Результат.СуммаОстаток Тогда
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("По сотруднику %1 неверная сумма к выплате, начислено %2", Результат.Сотрудник, Результат.СуммаОстаток));
			Отказ = Истина;
		КонецЕсли;

		Если Отказ Тогда
			Продолжить;
		КонецЕсли;

		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Сотрудник = Результат.Сотрудник;
		Движение.ВидРасчета = Результат.ВидРасчета;
		Движение.Сумма = Результат.Сумма;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
